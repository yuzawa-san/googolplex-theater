plugins {
	id "net.researchgate.release" version "2.8.1"
	id "com.google.protobuf" version "0.8.17"
	id "com.diffplug.spotless" version "5.15.0"
	id "com.github.spotbugs" version "4.7.5"
	id "com.github.ben-manes.versions" version "0.39.0"
	id "com.github.jakemarsden.git-hooks" version "0.0.2"
	id 'com.github.jk1.dependency-license-report' version '2.0'
	id "nebula.deb" version "9.0.0"
}

apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "application"
apply plugin: "jacoco"
apply plugin: "nebula.ospackage"

sourceCompatibility = 1.8
targetCompatibility = 1.8

description="Persistently maintain multiple Chromecasts as digital signage without a browser."

repositories {
	mavenCentral()
}

release {
	tagTemplate = 'v${version}'
}

jacocoTestReport {
	reports {
		xml.required = true
		html.required = true
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: '**/*Wire**')
		}))
	}
}
check.dependsOn jacocoTestReport

licenseReport {
	renderers = [new com.github.jk1.license.render.TextReportRenderer()]
}

sourceSets {
	main {
		java {
			srcDirs "${protobuf.generatedFilesBaseDir}/main/java"
		}
	}
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:3.18.0"
	}
	generateProtoTasks {
		all().each { task -> }
		ofSourceSet('main')
	}
}

// spotless requires java 11 or later
if (JavaVersion.current().isJava11Compatible()) {
	spotless {
		java {
			targetExclude("**/Wire.java")
			removeUnusedImports()
			googleJavaFormat()
		}
		format 'gradle', {
			target '**/*.gradle'
			trimTrailingWhitespace()
			indentWithTabs()
		}
		format 'proto', {
			target '**/*.proto'
			trimTrailingWhitespace()
			indentWithTabs()
		}
	}
}

dependencies {
	implementation(platform('org.apache.logging.log4j:log4j-bom:2.14.1'))
	implementation(platform('io.netty:netty-bom:4.1.68.Final'))
	implementation(platform('io.vertx:vertx-stack-depchain:4.1.4'))
	implementation(platform('com.fasterxml.jackson:jackson-bom:2.12.5'))
	implementation 'org.apache.logging.log4j:log4j-api'
	implementation 'org.apache.logging.log4j:log4j-core'
	implementation 'org.apache.logging.log4j:log4j-slf4j-impl'
	implementation 'org.slf4j:slf4j-api:1.7.32'
	implementation 'com.google.protobuf:protobuf-java:3.18.0'
	implementation 'io.vertx:vertx-core'
	implementation 'io.vertx:vertx-web'
	implementation 'io.vertx:vertx-web-templ-handlebars'
	implementation 'io.netty:netty-common'
	implementation 'io.netty:netty-transport'
	implementation 'io.netty:netty-codec'
	implementation 'io.netty:netty-handler'
	implementation 'com.fasterxml.jackson.core:jackson-core'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'org.jmdns:jmdns:3.5.7'
	implementation 'info.picocli:picocli:4.6.1'
	implementation 'org.bouncycastle:bcpkix-jdk15on:1.69'
	testImplementation 'org.mockito:mockito-core:3.12.4'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.0'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.0'
	testImplementation 'io.vertx:vertx-junit5'
	testImplementation 'io.vertx:vertx-web-client'
	testImplementation 'com.google.jimfs:jimfs:1.2'
}

test {
	useJUnitPlatform()
}

def getGitHash = { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'rev-parse', 'HEAD'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

mainClassName = 'com.jyuzawa.googolplex_theater.GoogolplexTheater'

jar {
	manifest {
		attributes(
			'Built-By'               : System.properties['user.name'],
			'Build-Timestamp'        : new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
			'Specification-Version'  : project.version,
			'Implementation-Version' : getGitHash(),
			'Created-By'             : "Gradle ${gradle.gradleVersion}",
			'Build-Jdk'              : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
			'Build-OS'               : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
		)
	}
}
jar.dependsOn(generateLicenseReport)

distributions {
	main {
		contents {
			from 'LICENSE'
			from 'README.md'
			from "${project.buildDir}/reports/dependency-license/THIRD-PARTY-NOTICES.txt"
		}
	}
}

task buildDocker(type:Exec) {
	dependsOn installDist
	def args = ['docker','buildx','build','--platform','linux/amd64,linux/arm/v7,linux/arm64/v8']
	if (!version.contains('-SNAPSHOT')) {
		args += ['-t','yuzawa/googolplex-theater:latest']
	}
	args += ['-t',"yuzawa/googolplex-theater:${version}",'--push','.']
	commandLine args
}

buildDeb {
	release '1'
	dependsOn installDist
	license "MIT"
	url "https://github.com/yuzawa-san/googolplex-theater"
	packager "James Yuzawa"
	into '/opt/googolplex-theater'
	user 'googolplex-theater'
	permissionGroup 'googolplex-theater'
	requires("default-jre-headless")
	from("${project.buildDir}/install/googolplex-theater") {
		exclude 'conf'
		exclude 'service'
		fileMode 0755
	}
	from("${project.buildDir}/install/googolplex-theater/conf") {
		fileType CONFIG | NOREPLACE
		fileMode 0644
		into 'conf'
	}
	configurationFile("/opt/googolplex-theater/conf/cast_config.json")
	from("${projectDir}/deb/googolplex-theater.service") {
		fileType CONFIG | NOREPLACE
		addParentDirs = false
		fileMode 0644
		into "/usr/lib/systemd/system"
	}
	configurationFile("/usr/lib/systemd/system/googolplex-theater.service")
	preInstall file("${projectDir}/deb/preinst")
	postInstall file("${projectDir}/deb/postinst")
	preUninstall file("${projectDir}/deb/prerm")
	postUninstall file("${projectDir}/deb/postrm")
}

spotbugs {
	ignoreFailures = true
	excludeFilter = file('spotbugs_exclude.xml')
}

spotbugsMain {
	reports {
		xml.enabled = false
		html.enabled = true
	}
}

gitHooks {
	hooks = ['pre-push': 'spotlessCheck']
}