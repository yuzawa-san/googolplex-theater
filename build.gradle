plugins {
	id "net.researchgate.release" version "2.8.1"
	id "com.google.protobuf" version "0.8.13"
	id "com.diffplug.spotless" version "5.7.0"
	id "com.github.spotbugs" version "4.5.1"
	id "com.github.ben-manes.versions" version "0.33.0"
	id "com.github.jakemarsden.git-hooks" version "0.0.2"
	id 'com.github.jk1.dependency-license-report' version '1.16'
}

apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "application"
apply plugin: "jacoco"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}

release {
	tagTemplate = 'v${version}'
}

jacocoTestReport {
	reports {
		xml.enabled true
		html.enabled true
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: '**/*Wire**')
		}))
	}
}
check.dependsOn jacocoTestReport

licenseReport {
	renderers = [new com.github.jk1.license.render.TextReportRenderer()]
}

sourceSets {
	main {
		java {
			srcDirs "${protobuf.generatedFilesBaseDir}/main/java"
		}
	}
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:3.13.0"
	}
	generateProtoTasks {
		all().each { task -> }
		ofSourceSet('main')
	}
}

spotless {
	java {
		targetExclude("**/Wire.java")
		removeUnusedImports()
		googleJavaFormat()
	}
	format 'gradle', {
		target '**/*.gradle'
		trimTrailingWhitespace()
		indentWithTabs()
	}
	format 'proto', {
		target '**/*.proto'
		trimTrailingWhitespace()
		indentWithTabs()
	}
}

dependencies {
	implementation 'org.apache.logging.log4j:log4j-api:2.13.3'
	implementation 'org.apache.logging.log4j:log4j-core:2.13.3'
	implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.13.3'
	implementation 'org.slf4j:slf4j-api:1.7.30'
	implementation 'com.google.protobuf:protobuf-java:3.13.0'
	implementation 'io.vertx:vertx-core:3.9.4'
	implementation 'io.vertx:vertx-web:3.9.4'
	implementation 'io.vertx:vertx-web-templ-handlebars:3.9.4'
	implementation 'io.netty:netty-common:4.1.53.Final'
	implementation 'io.netty:netty-transport:4.1.53.Final'
	implementation 'io.netty:netty-codec:4.1.53.Final'
	implementation 'io.netty:netty-handler:4.1.53.Final'
	implementation 'com.fasterxml.jackson.core:jackson-core:2.11.3'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.3'
	implementation 'org.jmdns:jmdns:3.5.6'
	implementation 'commons-cli:commons-cli:1.4'
	testImplementation 'org.mockito:mockito-core:3.5.15'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
	testImplementation 'io.vertx:vertx-junit5:3.9.4'
	testImplementation 'io.vertx:vertx-junit5-web-client:3.9.4'
}

test {
	useJUnitPlatform()
}

def getGitHash = { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'rev-parse', 'HEAD'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

mainClassName = 'com.jyuzawa.googolplex_theater.GoogolplexTheater'

jar {
	manifest {
		attributes(
			'Built-By'               : System.properties['user.name'],
			'Build-Timestamp'        : new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
			'Specification-Version'  : version,
			'Implementation-Version' : getGitHash(),
			'Created-By'             : "Gradle ${gradle.gradleVersion}",
			'Build-Jdk'              : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
			'Build-OS'               : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
		)
	}
}
jar.dependsOn(generateLicenseReport)

distributions {
	main {
		contents {
			from 'LICENSE'
			from 'README.md'
			from "${project.buildDir}/reports/dependency-license/THIRD-PARTY-NOTICES.txt"
		}
	}
}

spotbugs {
	ignoreFailures = true
	excludeFilter = file('spotbugs_exclude.xml')
}

spotbugsMain {
	reports {
		xml.enabled = false
		html.enabled = true
	}
}

gitHooks {
	hooks = ['pre-push': 'spotlessCheck']
}